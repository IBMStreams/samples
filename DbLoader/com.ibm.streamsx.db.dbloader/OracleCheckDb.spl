
/* Copyright (C) 2013-2014, International Business Machines Corporation  */
/* All Rights Reserved                                	                 */
namespace com.ibm.streamsx.db.dbloader ;

/** 
 * The composite **ORACLECheckDb** checks the state of Oracle database. 
 * 
 * It returns the status of Oracle database as boolean. 
 * 
 * @input ConfigurationData (database name,  database user, database password)
 * @output DbState boolean connected database status
 * @output OracleCheckLog rstring result (log information)
 */
composite ORACLECheckDb(output DbState, OracleCheckLog ; input ConfigurationData)
{
	graph

	/**
		 * CreateORACLECheckDbCommand
		 * Creates a sqlplus command to check the state of Oracle database. 
		 */
		stream<rstring command, int64 counter> CreateORACLECheckDbCommand = Custom(ConfigurationData)
		{
			logic
				state :
				{
					mutable rstring cmd ;
				}

				onTuple ConfigurationData :
				{
					printStringLn("CreateORACLECheckDbCommand " +(rstring) ConfigurationData) ;
					cmd = "echo 'select status from v$instance;' | sqlplus -s " + configData.dbUser + "/" +
						configData.dbPass + "@" + configData.dbName ;
					submit({ command = cmd, counter = configData.counter }, CreateORACLECheckDbCommand) ;
				}

		}

		/**
		 * ORACLECheckDb
		 * Execute a sqlplus command via Linux Shell to check the state of Oracle database 
		 */
		(stream<rstring result> ORACLECheckDb ; stream<rstring result> OracleCheckLog) =
			ExecuteCommand(CreateORACLECheckDbCommand)
		{
		}

		/**
		 * CheckOracleDbStatus
		 * Check the state of Oracle database 
		 * If result is OPEN the Oracle database is running
		 * And the database user name and password are correct  
		 */
		stream<boolean connected> CheckOracleDbStatus = Custom(ORACLECheckDb)
		{
			logic
				state :
				{
					mutable int32 successfulConnection ;
					mutable rstring logMessage ;
				}

				onTuple ORACLECheckDb :
				{
					if(length(result) > 2)
					{
						successfulConnection = findFirst(result, "OPEN", 0) ;
						if(successfulConnection == - 1)
						{
							logMessage = "Oracle check database failed." + result ;
							log(Sys.error, logMessage) ;
							printStringLn(logMessage) ;
							submit({ connected = false }, CheckOracleDbStatus) ;
						}

						else // successfull

						{
							logMessage = "Oracle check database completed successfully." ;
							log(Sys.info, logMessage) ;
							printStringLn(logMessage) ;
							submit({ connected = true }, CheckOracleDbStatus) ;
						}

					}

				}

		}

		/**
		 * DbState
		 * Send the tuple connected as DbState of ORACLECheckDb composite
		 */
		stream<boolean connected> DbState = Custom(CheckOracleDbStatus)
		{
			logic
				onTuple CheckOracleDbStatus : submit(CheckOracleDbStatus, DbState) ;
		} // End of DbState = Custom(CheckOracleDbStatus)

	config
		placement : partitionColocation("ORACLECheckDb") ;
}
/** End of ORACLECheckDb composite */ 